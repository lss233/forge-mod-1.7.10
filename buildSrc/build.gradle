plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

group = 'com.lss233.forge'
version = '1.2-1.0.4-lss233-rev'

archivesBaseName = 'ForgeGradle'
targetCompatibility = '1.6'
sourceCompatibility = '1.6'

repositories {
    mavenLocal()
    maven {
        name = "aliyun-mirror"
        url = "https://maven.aliyun.com/repository/public"
    }
    maven {
        name = "forge"
        url = "https://lss233.littleservice.cn/repositories/minecraft"
    }
}

jar {
    manifest {
        attributes 'version': project.version, 'javaCompliance': project.targetCompatibility
        attributes 'group': project.group
        attributes 'Implementation-Version': project.version
    }
}

configurations {
    compileOnly
}

dependencies {
    compile gradleApi()

    // moved to the beginning to be the overrider
    compile 'org.ow2.asm:asm-debug-all:5.0.3'
    compile 'com.google.guava:guava:18.0'

    compile 'net.sf.opencsv:opencsv:2.3' // reading CSVs.. also used by SpecialSource
    compile 'com.cloudbees:diff4j:1.1' // for difing and patching
    compile 'com.github.abrarsyed.jastyle:jAstyle:1.2' // formatting
    compile 'net.sf.trove4j:trove4j:2.1.0' // because its awesome.

    compile 'com.github.jponge:lzma-java:1.3' // replaces the LZMA binary
    compile 'com.nothome:javaxdelta:2.0.1' // GDIFF implementation for BinPatches
    compile 'com.google.code.gson:gson:2.2.4' // Used instead of Argo for buuilding changelog.
    compile 'com.github.tony19:named-regexp:0.2.3' // 1.7 Named regexp features

    compile 'net.md-5:SpecialSource:1.7.3' // deobf and reobs

    // because curse
    compile 'org.apache.httpcomponents:httpclient:4.3.3'
    compile 'org.apache.httpcomponents:httpmime:4.3.3'

    // mcp stuff
    compile 'de.oceanlabs.mcp:RetroGuard:3.6.6'
    compile 'de.oceanlabs.mcp:mcinjector:3.2-SNAPSHOT'
    compile 'net.minecraftforge.srg2source:Srg2Source:3.2-SNAPSHOT'

    //Stuff used in the GradleStart classes
    compileOnly 'com.mojang:authlib:1.5.16'
    compileOnly "net.minecraft:launchwrapper:1.11"
}

compileJava {
    options.deprecation = true
    //options.compilerArgs += ["-Werror", "-Xlint:unchecked"]
}

javadoc {
    classpath += configurations.compileOnly
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc
    classifier = "javadoc"
}

task sourcesJar(type: Jar, dependsOn: javadoc) {
    from sourceSets.main.allSource
    classifier = "sources"
}

artifacts {
    archives jar
}

test {
    if (project.hasProperty("filesmaven")) // disable this test when on the forge jenkins
    {
        exclude "**/ExtensionMcpMappingTest*"
        exclude "**/ExtensionForgeVersionTest*"
    }
}